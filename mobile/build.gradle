apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'com.android.application'
apply plugin: 'hugo'//for method logging (only in debug builds)
apply plugin: 'com.github.ben-manes.versions'// to check for dependency updates

advancedVersioning {
    codeOptions {
        versionCodeType org.moallemi.gradle.internal.VersionCodeType.DATE
    }
    nameOptions {
        versionMajor 0
        versionMinor 2
        versionPatch 9
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildToolsVersion
    def buildNumber = (System.getenv("BUILD_NUMBER") ?: advancedVersioning.versionCode) as int

    defaultConfig {
        applicationId "pct.droid"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode buildNumber
        versionName advancedVersioning.versionName
        multiDexEnabled true
    }

    signingConfigs {
        mainConfig {
            keyAlias System.getenv("S_ALIAS").toString() ?: ""
            keyPassword System.getenv("S_KEYPASS").toString() ?: ""
            storeFile System.getenv("S_KEYSTORE").toString() ? file(System.getenv("S_KEYSTORE").toString()) : file("")
            storePassword System.getenv("S_KEYPASS").toString() ?: ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.mainConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".dev"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }

    splits {
        abi {
            enable true
            universalApk false
            reset()
            include 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
        }
    }

    project.ext.versionCodes = ["armeabi": 1, "armeabi-v7a": 2, "arm64-v8a": 3, "x86": 4, "x86_64": 4]
}

//to check for dependency updates,
//run ./gradlew dependencyUpdates -Drevision=release
dependencies {
    compile 'com.android.support:multidex:1.0.1'

    compile "com.android.support:support-v4:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:mediarouter-v7:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:support-annotations:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:design:${rootProject.ext.appCompatVersion}"

    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile "com.jakewharton:butterknife:${rootProject.ext.butterknifeVersion}"
    compile 'com.github.nirhart:parallaxscroll:1.0'
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.gu:option:1.3'
    compile 'net.rdrei.android.dirchooser:library:3.0@aar'
    compile 'com.github.johnkil.android-robototextview:robototextview:2.4.3'
    
    compile 'com.sothree.slidinguppanel:library:3.2.0'

    compile project(':base')

    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"

    provided "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}"

}
